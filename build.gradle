plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'io.github.enoughsdv'
    version = '1.0-SNAPSHOT'

    java.sourceCompatibility = JavaVersion.VERSION_1_8
    java.targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    }

    dependencies {
        implementation('net.dv8tion:JDA:5.0.0-beta.13') {
            exclude module: 'opus-java'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    jar {
        archiveClassifier = 'default'
    }

    shadowJar {
        archiveClassifier = null
        archiveFileName = "discordremote-${project.name}-${project.version}.jar"
    }

    artifacts {
        archives shadowJar
    }

    build.dependsOn(shadowJar)
}